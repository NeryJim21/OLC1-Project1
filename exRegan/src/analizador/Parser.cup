/*
* ORGANIZACIÓN DE LENGUAJES Y COMPILADORES 1 SECCIÓN C
* PROYECTO 1 - EXREGAN
* PRIMER SEMESTRE 2023
* NERY JIMÉNEZ - 201700381
*/

package analizador;

import java_cup.runtime.*;
import java.util.ArrayList;
import java.util.List;
import Errors.Errors;

parser code
{:

    //Lista para errores
    public ArrayList<Errors> Errores = new ArrayList();

    //Método para manejar algún error sintáctico
    public void syntax_error(Symbol s){ 
        Errores.add(new Errors("Sintáctico", "Error de sintaxis detectado. Símbolo: " + s.value, s.left + "", s.right + ""));
    } 

    //Método errores sintácticos en el que ya no es posible recuperar errores
    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{ 
        System.out.println("Error síntactico irrecuperable en la Línea " + (s.left)+ " Columna "+s.right+". Componente " + s.value + " no reconocido."); 
    }

    public ArrayList<Errors> getErrores(){
        return Errores;
    }
:}

terminal String DOSPUNTOS, PTCOMA,LLAVABRE,LLAVCIERRA;
terminal String MAS,POR, PORCENTAJE;
terminal String DECIMAL, ID, ASCII;
terminal String PR_CONJ, FLECHA, COLOCHO, PUNTO, OR, COMA, INTERROG, LN, COMSIMPLE;
terminal String COMDOBLE, COMILLA;

non terminal ini, cuerpo, conjunto, definicion, expresion, cuerpoExpresion, especiales, definido;
non terminal lexemas, operaciones, usoConjuntos, data, simbolos, asciiSymb, asciiComplement;

start with ini;

ini ::= LLAVABRE cuerpo LLAVCIERRA;

cuerpo ::= conjunto
                |cuerpo conjunto
                | expresion
                | cuerpo expresion
                | lexemas
                | cuerpo lexemas
                | cuerpo error
;

conjunto ::= PR_CONJ DOSPUNTOS ID FLECHA definicion PTCOMA
                  | error PTCOMA
;

definicion ::= ID COLOCHO ID
                    | asciiSymb COLOCHO asciiSymb
                    | definido
;

definido ::= ID
                 | definido ID
                 | definido COMA ID
                 | asciiSymb
                 | definido asciiComplement
                 | definido COMA asciiComplement
                 | especiales
                 | definido COMA especiales
;

asciiComplement ::= PORCENTAJE
                                | POR
                                | MAS
                                | PUNTO
                                | DOSPUNTOS
                                | LLAVABRE
                                | LLAVCIERRA
                                | OR
                                | INTERROG
;

expresion ::= ID FLECHA cuerpoExpresion PTCOMA
;

cuerpoExpresion ::=  operaciones
                                | cuerpoExpresion operaciones
                                | usoConjuntos
                                | cuerpoExpresion usoConjuntos
                                | COMILLA data COMILLA
                                | cuerpoExpresion COMILLA data COMILLA
;

operaciones ::= PUNTO
                        | OR
                        | POR
                        | MAS
                        | INTERROG
;

usoConjuntos ::= LLAVABRE ID LLAVCIERRA
;

especiales ::= LN
                    | COMSIMPLE
                    | COMDOBLE
;

data ::=  ID
            | data ID
            | DECIMAL
            | data DECIMAL
            | especiales
            | data especiales
            | asciiSymb
            | data asciiSymb
;

simbolos ::=  PORCENTAJE
                    | POR
                    | MAS
                    | COMA
                    | PUNTO
                    | DOSPUNTOS
                    | PTCOMA
                    | LLAVABRE
                    | LLAVCIERRA
                    | OR
                    | INTERROG
;

asciiSymb ::= ASCII
                    | simbolos
;
lexemas ::= PORCENTAJE PORCENTAJE
                 | ID DOSPUNTOS COMILLA data COMILLA PTCOMA
;
